plugins {
  id 'groovy'
  id 'maven'
  id 'signing'
  id 'java-gradle-plugin'
  id "com.gradle.plugin-publish" version "0.9.9"
}

project.version = '1.13.0'
project.group = 'org.jastadd'
project.archivesBaseName = 'jastaddgradle'

// Ensure the plugin runs with Java 7.
sourceCompatibility = targetCompatibility = '1.7'

repositories {
  jcenter()
}

dependencies {
  compile gradleApi()
  compile localGroovy()

  testCompile('org.spockframework:spock-core:1.0-groovy-2.4') {
    exclude module: 'groovy-all'
  }
}

test {
  testLogging.exceptionFormat = "full"
}

task wrapper(type: Wrapper) {
  gradleVersion = '3.5'
}

task javadocJar(type: Jar) {
  classifier = 'javadoc'
  from javadoc
}

task sourcesJar(type: Jar) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

artifacts {
  archives javadocJar, sourcesJar
}

signing {
  // Skip signing if there is no key configured:
  required { project.hasProperty('signing.keyId') }
  sign configurations.archives
}

if (project.hasProperty('ossrhUsername')) {
  uploadArchives {
    repositories.mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2') {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots') {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      pom.project {
        name 'JastAddGradle'
        packaging 'jar'
        description 'A Gradle build plugin for building JastAdd projects.'
        url 'https://github.com/jastadd/jastaddgradle'
        licenses {
          license {
            name 'Modified BSD License'
            url 'http://opensource.org/licenses/BSD-3-Clause'
            distribution 'repo'
          }
        }
        developers {
          developer {
            name 'Jesper Ã–qvist'
            email 'jesper.oqvist@cs.lth.se'
          }
        }
        scm {
          connection 'scm:git:https://bitbucket.org/joqvist/jastaddgradle.git'
          url 'https://bitbucket.org/joqvist/jastaddgradle'
        }
      }
    }
  }
}

pluginBundle {
  website = 'https://github.com/jastadd/jastaddgradle'
  vcsUrl = 'https://github.com/jastadd/jastaddgradle'
  description = 'Modular JastAdd project build plugin.'
  tags = [ 'jastadd', 'codegen' ]

  plugins {
    jastaddPlugin {
      id = 'org.jastadd'
      displayName = 'JastAdd Gradle plugin'
    }
  }
}
