plugins {
  id 'groovy'
  id 'maven-publish'
  id 'signing'
  id 'java-gradle-plugin'
  id "com.gradle.plugin-publish" version "0.14.0"
}

group = 'org.jastadd'
version = '1.14.2'

// Ensure the plugin runs with Java 7.
sourceCompatibility = targetCompatibility = '1.7'

repositories {
  mavenCentral()
}

dependencies {
  implementation gradleApi()
  implementation localGroovy()

  testImplementation('org.spockframework:spock-core:1.0-groovy-2.4') {
    exclude module: 'groovy-all'
  }
}

test {
  testLogging.exceptionFormat = "full"
}

java {
  withJavadocJar()
  withSourcesJar()
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      artifactId = 'jastaddgradle'
      from components.java

      versionMapping {
        usage('java-api') {
          fromResolutionOf('runtimeClasspath')
        }
        usage('java-runtime') {
          fromResolutionResult()
        }
      }

      pom {
        name = 'JastAddGradle'
        description = 'A Gradle build plugin for building JastAdd projects.'
        url = 'https://github.com/jastadd/jastaddgradle'
        licenses {
          license {
            name = 'Modified BSD License'
            url = 'http://opensource.org/licenses/BSD-3-Clause'
            distribution = 'repo'
          }
        }
        developers {
          developer {
            name = 'Jesper Ã–qvist'
            email = 'jesper.oqvist@cs.lth.se'
          }
        }
        scm {
          connection = 'scm:git:https://bitbucket.org/joqvist/jastaddgradle.git'
          url = 'https://bitbucket.org/joqvist/jastaddgradle'
        }
      }
    }
  }
  repositories {
    maven {
      url = 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
      credentials {
        username = ossrhUsername
        password = ossrhPassword
      }

      // Local repository = 'localPluginRepo'
      // Snapshot repoitory = 'https://oss.sonatype.org/content/repositories/snapshots'
    }
  }
}

signing {
  // Skip signing if there is no key configured:
  required { project.hasProperty('signing.keyId') }
  sign publishing.publications.mavenJava
}

pluginBundle {
  website = 'https://github.com/jastadd/jastaddgradle'
  vcsUrl = 'https://github.com/jastadd/jastaddgradle'
  description = 'Modular JastAdd project build plugin.'
  tags = [ 'jastadd', 'codegen' ]
}

gradlePlugin {
  plugins {
    jastaddPlugin {
      id = 'org.jastadd'
      displayName = 'JastAdd Gradle plugin'
      description = 'Plugin for building JastAdd projects with Gradle.'
      implementationClass = 'org.jastadd.JastAddPlugin'
    }
  }
}

javadoc {
  if(JavaVersion.current().isJava9Compatible()) {
    options.addBooleanOption('html5', true)
  }
}
